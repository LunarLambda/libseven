pkgname='libseven-git'
pkgver=r138.e2479f0
pkgrel=1
pkgdesc='from-scratch library for GBA development'
arch=('any')
url='https://github.com/LunarLambda/libseven'
license=('MPL2')
groups=('gba-dev')
depends=('devkitARM' 'devkitarm-rules' 'devkitarm-crtls')
makedepends=('git')
provides=('libseven')
conflicts=('libseven')
options=('!strip' 'libtool' 'staticlibs')
source=('libseven::git+https://github.com/LunarLambda/libseven')
md5sums=('SKIP')

pkgver() {
    cd libseven
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
    make -C libseven -f Makefile.dkp
}

package() {
    make -C libseven -f Makefile.dkp DESTDIR=$pkgdir install
}
